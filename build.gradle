plugins {
    id 'application'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'org.jreleaser' version '1.17.0'
}

group = 'io.github.sogis'

version '0.0.10'+System.getProperty('release','-SNAPSHOT')

def myArtifactId = "dmav"

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
    options.compilerArgs.addAll(['--release', '11'])
}

repositories {
    mavenCentral()
    maven { url 'https://jars.interlis.ch/' }
}

dependencies {
    testImplementation libs.junit.jupiter
    testImplementation "ch.interlis:ilivalidator:1.14.5"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'info.picocli:picocli:4.7.6'
    implementation 'info.picocli:picocli-codegen:4.7.6'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'
    
    implementation libs.saxon.he
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'ch.so.agi.dmav.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

run {
    //args = ["--help"]
    args = ["merge", "--config=myconfigAAAAA.ini", "--fosnr=449", "--out=/Users/stefan/tmp"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "dmav"
                url = "https://github.com/sogis/dmav"
                description = "dmav"
                scm {
                    connection = "scm:git:https@github.com/sogis/dmav"
                    developerConnection = "scm:git:https@github.com/sogis/dmav"
                    url = "https://github.com/sogis/dmav"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/sogis/dmav/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "edigonzales"
                        name = "Stefan Ziegler"
                    }
                }
            }
        }
    }
}

if (!version.toString().endsWith("SNAPSHOT")) {
    signing {
        sign publishing.publications.mavenJava
    }
}

nexusPublishing {
    repositories {
        sonatype { 
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
            password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
        }
    }
}

jreleaser {
    gitRootSearch = true
    
    project {
        name = myArtifactId
        description = 'Werkzeugkasten f√ºr das Handling mit DMAV-Transferdateien.'
        website = 'https://agi.so.ch'
        authors = ['edigonzales']
        license = 'MIT'
        docsUrl = 'https://github.com/sogis/dmav'
        copyright = '2024 Stefan Ziegler'
    }
    release {
        github {
            repoOwner = 'sogis'
            overwrite = true
            apiEndpoint = 'https://api.github.com'
        }
    }
    distributions {
        appJvm {
            distributionType = 'SINGLE_JAR'
            stereotype = 'CLI'
            artifact {
                path = "build/libs/dmav-${version}-all.jar"
            }
            java {
                groupId = 'io.github.sogis'
                artifactId = myArtifactId
                version = '11'
            }
        }
    }
}
